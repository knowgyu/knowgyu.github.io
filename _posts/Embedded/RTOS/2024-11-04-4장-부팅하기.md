---
title: "[RTOS개발] 4장 부팅하기"
author: knowgyu
description: " "
date: 2024-10-06 11:08:14 +0900
math: true
categories: [Embedded System, 임베디드 OS 개발 프로젝트]
tags: [Embedded System, RTOS, Firmware, OS]
---


![image.png](/assets/img/OS/OS000.jpg){: .w-25}
<br>

**본 프로젝트는 이만우 저자님의 "임베디드 OS 개발 프로젝트" 교재를 따라 RTOS를 만드는 것을 목표로 합니다.**

<br>

***
본 장에서는 시스템의 전원이 켜진 후 초기화 작업을 완료하고 대기 상태(idle)에 도달하는 **부팅 과정**에 대해 다룹니다.

## 서론 📚

***

컴퓨터를 켰을 때 운영체제의 화면이나 바탕화면이 나타나는 과정을 일반적으로 **부팅**이라고 합니다. 💻

펌웨어 관점에서 부팅은 특정한 규칙이 정해져 있지 않지만, 시스템 전원이 켜진 후 모든 초기화 작업을 완료하고 펌웨어가 대기 상태에 도달할 때까지의 과정을 의미합니다. 또한, ARM 코어가 리셋 익셉션 핸들러를 모두 처리한 후, 본격적으로 C 언어 코드로 전환되기 직전의 상태도 부팅 과정에 포함됩니다.

## 메모리 설계 🧠

***

실행 파일은 메모리를 크게 세 가지 영역으로 나눠 사용합니다.

1. **text 영역:** 코드가 위치하며 변경이 불가능합니다.
2. **data 영역:** 초기화된 전역 변수가 저장됩니다.
3. **bss 영역:** 초기화되지 않은 전역 변수가 저장됩니다. 빌드된 바이너리 파일에는 심벌과 크기 정보만 포함됩니다.

이제 **text**, **data**, **bss** 영역을 어떻게 배치할지 고민해야 합니다.

- **임베디드 시스템의 메모리 유형에 따른 배치 전략:**
  
  - **빠른 메모리:** **text 영역** 및 일부 성능에 민감한 **data 영역**을 배치합니다.
  - **용량 큰 메모리:** 나머지 **data 영역**과 **bss 영역**을 배치합니다.

하지만, **QEMU**는 이러한 메모리 구분이 없으므로 순차적으로 배치하겠습니다.

### Text 영역의 크기 설정 📏

리눅스 같은 거대한 운영체제를 사용하지 않기에, RTOS를 사용하는 펌웨어는 많아야 수십 KB 정도면 충분합니다.

본 프로젝트에서는 넉넉하게 **1MB**를 할당하겠습니다.

익셉션 벡터 테이블을 **text 영역**에 포함시킬 것이므로, 시작 주소는 `0x0000 0000`이며, **1MB**이기에 끝 주소는 `0x000F FFFF`입니다.

### Data 영역과 BSS 영역의 크기 설정 📐

데이터의 성격에 따라 데이터를 할당해야 합니다.

- **데이터의 형태:** 동작 모드별 스택, 테스크 스택, 전역 변수, 동적 메모리 할당 영역
- **데이터의 속성:** 성능 중시 데이터, 큰 공간이 필요한 데이터, 공유 데이터

> - USR, SYS(2MB) : 0x0010 0000 ~ 0x002F FFFF  
> - SVC(1MB) : 0X0030 0000 ~ 0X003F FFFF  
> - IRQ(1MB) …  
> - FIQ(1MB) …  
> - ABT(1MB) …  
> - UND(1MB) …
{: .prompt-info }

개별 동작 모드마다 각 **1MB**씩 할당했습니다. USR과 SYS 모드는 메모리 공간과 레지스터를 모두 공유하기에 하나로 묶어 **2MB**를 할당했습니다.

RTOS를 개발할 것이기에 RTOS 위에서 동작할 **테스크(Task) 스택 영역**도 고려해야 합니다. 테스크마다 각 **1MB**씩 할당할 계획이므로 총 **64MB**를 배정하겠습니다.

→ 본 프로젝트의 RTOS는 최대 **64개**의 테스크를 지원하게 됩니다.

- **메모리 설계**

![메모리 설계](/assets/img/OS/os301.png)

## 익셉션 벡터 테이블 만들기 🛠️

***

설계를 완료한 후, **익셉션 핸들러**를 코드로 구현하겠습니다.

### 익셉션 벡터 테이블 초기 코드 `Entry.S` 📄

```bash
.text
	.code 32

	.global vector_start
	.global vector_end

	vector_start:
		LDR		PC, reset_handler_addr
		LDR		PC, undef_handler_addr
		LDR		PC, svc_handler_addr
		LDR		PC, pftch_abt_handler_addr
		LDR		PC, data_abt_handler_addr
		B		.
		LDR		PC, irq_handler_addr
		LDR		PC, fiq_handler_addr

		reset_handler_addr: 	.word reset_handler
		undef_handler_addr: 	.word dummy_handler
		svc_handler_addr: 		.word dummy_handler
		pftch_abt_handler_addr: .word dummy_handler
		data_abt_handler_addr:  .word dummy_handler
		irq_handler_addr:		.word dummy_handler
		fiq_handler_addr:		.word dummy_handler
	vector_end:

	reset_handler:
		LDR		R0, =0x10000000
		LDR		R1, [R0]

	dummy_handler:
		B .
.end
```

> 붙여넣기 시 vi 명령어로 `:set paste` 후 붙여넣으면 들여쓰기 오류를 방지할 수 있습니다.
{: .prompt-info }

우선, 각 핸들러로 점프하는 코드만 작성하고, 핸들러는 작성하지 않았습니다.

리셋 익셉션 핸들러에는 `SYS_ID`를 읽는 코드가 있으므로, 3장에서와 같이 `R0`과 `R1`에 값이 있는지 확인하고, 코드를 이어서 작성하겠습니다.

```bash
$ make debug
...
$ make gdb
...
(gdb) target remote:1234
(gdb) continue

^C  # 무한루프를 돌고 있으므로, Ctrl + C를 이용해 빠져나옵니다.
(gdb) i r
```

![디버깅 이미지](/assets/img/OS/os302.png)

이전 3.5절에서 확인한 결과와 같습니다.

## 익셉션 핸들러 만들기 🔧

***

핸들러를 작성하기 전에, **익셉션 핸들러**와 **뱅크드 레지스터**에 대한 ARM 아키텍처의 개념을 숙지하는 것이 좋습니다.

가장 먼저 작성할 핸들러는 **리셋 익셉션 핸들러**입니다. 이 핸들러는 메모리 맵을 설정하고, 동작 모드별 스택 주소를 **SP 레지스터**에 할당합니다. 모든 스택 설정이 완료되면 C 언어의 `main()` 함수로 진입하여 **C 언어로 임베디드 시스템을 제어**할 수 있습니다.

ARM은 총 **7개의 동작 모드**가 있으며, USR과 SYS 모드는 레지스터를 공유하여 **6개의 SP 레지스터**가 제공됩니다.

### 스택 만들기 🧩

이전 절에서 설계한 메모리 맵을 C 언어 코드로 구현합니다.

### 스택 주소를 정의 `MemoryMap.h` 📁

```bash
#define INST_ADDR_START     0
#define USRSYS_STACK_START  0x00100000
#define SVC_STACK_START     0x00300000
#define IRQ_STACK_START     0x00400000
#define FIQ_STACK_START     0x00500000
#define ABT_STACK_START     0x00600000
#define UND_STACK_START     0x00700000
#define TASK_STACK_START    0x00800000
#define GLOBAL_ADDR_START   0x04800000
#define DALLOC_ADDR_START   0x04900000

#define INST_MEM_SIZE       (USRSYS_STACK_START - INST_ADDR_START)
#define USRSYS_STACK_SIZE   (SVC_STACK_START - USRSYS_STACK_START)
#define SVC_STACK_SIZE      (IRQ_STACK_START - SVC_STACK_START)
#define IRQ_STACK_SIZE      (FIQ_STACK_START - IRQ_STACK_START)
#define FIQ_STACK_SIZE      (ABT_STACK_START - FIQ_STACK_START)
#define ABT_STACK_SIZE      (UND_STACK_START - ABT_STACK_START)
#define UND_STACK_SIZE      (TASK_STACK_START - UND_STACK_START)
#define TASK_STACK_SIZE     (GLOBAL_ADDR_START - TASK_STACK_START)
#define DALLOC_MEM_SIZE     (55 * 1024 * 1024)

#define USRSYS_STACK_TOP    (USRSYS_STACK_START + USRSYS_STACK_SIZE - 4)
#define SVC_STACK_TOP       (SVC_STACK_START + SVC_STACK_SIZE - 4)
#define IRQ_STACK_TOP       (IRQ_STACK_START + IRQ_STACK_SIZE - 4)
#define FIQ_STACK_TOP       (FIQ_STACK_START + FIQ_STACK_SIZE - 4)
#define ABT_STACK_TOP       (ABT_STACK_START + ABT_STACK_SIZE - 4)
#define UND_STACK_TOP       (UND_STACK_START + UND_STACK_SIZE - 4)
```

위 코드는 C 언어 헤더 파일이지만, GCC로 컴파일하면 `Entry.S` 어셈블리어 파일에서도 사용할 수 있습니다.

그리고, ARM의 `cspr`에 값을 설정해 동작 모드를 변경할 수 있는 값을 정의한 헤더 파일을 만들겠습니다.

### 동작 모드 전환 값 `ARMv7AR.h` 🔄

```bash
/* PSR Mode Bit Values */
#define ARM_MODE_BIT_USR 0x10
#define ARM_MODE_BIT_FIQ 0x11
#define ARM_MODE_BIT_IRQ 0x12
#define ARM_MODE_BIT_SVC 0x13
#define ARM_MODE_BIT_ABT 0x17
#define ARM_MODE_BIT_UND 0x1B
#define ARM_MODE_BIT_SYS 0x1F
#define ARM_MODE_BIT_MON 0x16
```

헤더 파일을 어셈블리어 코드에 포함시킵니다.

### 동작 모드 스택 초기화 리셋 익셉션 핸들러 `Entry.S` 📝

```bash
#include "ARMv7AR.h"
#include "MemoryMap.h"

.text
    .code 32

    .global vector_start
    .global vector_end

    vector_start:
        LDR PC, reset_handler_addr
        LDR PC, undef_handler_addr
        LDR PC, svc_handler_addr
        LDR PC, pftch_abt_handler_addr
        LDR PC, data_abt_handler_addr
        B   .
        LDR PC, irq_handler_addr
        LDR PC, fiq_handler_addr

        reset_handler_addr:     .word reset_handler
        undef_handler_addr:     .word dummy_handler
        svc_handler_addr:       .word dummy_handler
        pftch_abt_handler_addr: .word dummy_handler
        data_abt_handler_addr:  .word dummy_handler
        irq_handler_addr:       .word dummy_handler
        fiq_handler_addr:       .word dummy_handler
    vector_end:

    reset_handler:
        MRS r0, cpsr
        BIC r1, r0, #0x1F
        ORR r1, r1, #ARM_MODE_BIT_SVC
        MSR cpsr, r1
        LDR sp, =SVC_STACK_TOP

        MRS r0, cpsr
        BIC r1, r0, #0x1F
        ORR r1, r1, #ARM_MODE_BIT_IRQ
        MSR cpsr, r1
        LDR sp, =IRQ_STACK_TOP

        MRS r0, cpsr
        BIC r1, r0, #0x1F
        ORR r1, r1, #ARM_MODE_BIT_FIQ
        MSR cpsr, r1
        LDR sp, =FIQ_STACK_TOP

        MRS r0, cpsr
        BIC r1, r0, #0x1F
        ORR r1, r1, #ARM_MODE_BIT_ABT
        MSR cpsr, r1
        LDR sp, =ABT_STACK_TOP

        MRS r0, cpsr
        BIC r1, r0, #0x1F
        ORR r1, r1, #ARM_MODE_BIT_UND
        MSR cpsr, r1
        LDR sp, =UND_STACK_TOP

        MRS r0, cpsr
        BIC r1, r0, #0x1F
        ORR r1, r1, #ARM_MODE_BIT_SYS
        MSR cpsr, r1
        LDR sp, =USRSYS_STACK_TOP

    dummy_handler:
        B .
.end
```

> 헤더 파일의 위치를 어셈블러에 알려주지 않으면 심벌 정의 오류가 발생할 수 있습니다.
{: .prompt-info }

### Makefile 수정 🛠️

헤더 파일 경로를 지정하여 컴파일 오류를 방지합니다.

```bash
ARCH = armv7-a
MCPU = cortex-a8

CC = arm-none-eabi-gcc
AS = arm-none-eabi-as
LD = arm-none-eabi-ld
OC = arm-none-eabi-objcopy

LINKER_SCRIPT = ./navilos.ld

ASM_SRCS = $(wildcard boot/*.S)
ASM_OBJS = $(patsubst boot/%.S, build/%.o, $(ASM_SRCS))

INC_DIRS = include # 헤더파일이 있는 디렉토리를 지정

navilos = build/navilos.axf
navilos_bin = build/navilos.bin

.PHONY: all clean run debug gdb

all: $(navilos)

clean:
	@rm -fr build
	
run: $(navilos)
	qemu-system-arm -M realview-pb-a8 -kernel $(navilos)
	
debug: $(navilos)
	qemu-system-arm -M realview-pb-a8 -kernel $(navilos) -S -gdb tcp::1234,ipv4
	
gdb:
	gdb-multiarch $(navilos)
	
$(navilos): $(ASM_OBJS) $(LINKER_SCRIPT)
	$(LD) -n -T $(LINKER_SCRIPT) -o $(navilos) $(ASM_OBJS)
	$(OC) -O binary $(navilos) $(navilos_bin)
	
build/%.o: boot/%.S
	mkdir -p $(shell dirname $@)
	$(CC) -march=$(ARCH) -mcpu=$(MCPU) -I $(INC_DIRS) -c -g -o $@ $< # -I 옵션으로 추가하고, $(AS)를 $(CC)로 수정
```

> `#define`은 전처리기에 의해 처리되는데, `arm-none-eabi-as`는 어셈블러일 뿐 전처리를 지원하지 않기에, 전처리까지 진행하려면 `gcc`를 사용해야 합니다. <br>→ `$(AS)`에서 `$(CC)`로 수정
{: .prompt-info }

`$(CC)`로 수정하여 GCC를 사용함으로써, `-c` 옵션을 추가해 오브젝트 파일을 생성하도록 지시합니다.

### 스택 확인하기 🧐

빌드를 완료했으니, 이제 스택이 제대로 초기화되었는지 확인해 보겠습니다.

변수나 레지스터의 값을 출력할 수 없으므로, **gdb**를 이용해 확인합니다.

```bash
(gdb) target remote:1234
.
(gdb) s
.
(gdb) s
.
(gdb) s
.
(gdb) s
.
(gdb) s
.
(gdb) s
.
(gdb) i r
r0             0x400001d3          1073742291
r1             0x400001d3          1073742291
r2             0x0                 0
r3             0x0                 0
...
```

![스택 확인 이미지](/assets/img/OS/os303.png)

34번째 줄까지 실행되면 첫 번째 SVC 동작 모드 스택이 설정됩니다.

이전 SVC 모드 스택은 `0x0030 0000 ~ 0x003F FFFF`까지의 메모리 주소 영역을 가집니다.

또한, 스택과 스택 경계 사이에 **4바이트**를 비워두도록 크기를 설정했으므로, 스택 포인터에 저장되어야 할 값은 `0x003F FFFC`입니다.

위 이미지를 보면 `sp 0x3f fffc`라는 출력이 보입니다. 나머지 값들도 확인해보면, 값이 제대로 설정되었음을 알 수 있습니다.

> SYS 동작 모드를 가장 마지막에 설정했습니다. 스택 설정을 완료하면 RTOS로 진입하게 되는데, RTOS와 펌웨어의 기본 동작 모드가 SYS이기에, 추가 설정 없이 SYS 모드로 작업할 수 있습니다.
{: .prompt-info }

## 메인으로 진입하기 🎯

***

C 언어의 시작 지점은 일반적으로 `main()` 함수입니다. 컴파일러가 기본적으로 `main()`을 사용하기 때문에, 이를 유지하는 것이 좋습니다.

### 메인 함수로 점프하는 코드 추가 `Entry.S` 🔄

```bash
...(생략)
        MRS r0, cpsr
        BIC r1, r0, #0x1F
        ORR r1, r1, #ARM_MODE_BIT_SYS
        MSR cpsr, r1
        LDR sp, =USRSYS_STACK_TOP
                
                BL main     # 이거 추가
```

이 한 줄로 어셈블리어 코드에서 C 언어 코드로 전환됩니다.

이제, `boot/Main.c` 파일을 만들고 간단한 코드를 작성해보겠습니다.

### Main.c 파일 초기 코드 🖥️

```bash
#include "stdint.h"

void main(void){
	uint32_t* dummyAddr = (uint32_t*)(1024*1024*100);
	*dummyAddr = sizeof(long);
}
```

어셈블리어 코드에서 브랜치 명령(`BL`)으로 점프를 하려면, 점프 대상 레이블이 동일 파일에 있어야 합니다.

다른 파일에 있다면 링커가 접근할 수 있도록 레이블을 `.global`로 선언해야 합니다.

**컴파일러**는 C 언어 함수 이름을 링커가 자동으로 접근할 수 있는 전역 심벌로 만듭니다.

> 전역 심벌은 어셈블리어에서 `.global`이고, C 언어에서는 `extern`으로 선언된 이름입니다.  
> 
> 반대로 어셈블리어에서 `.global`로 선언한 이름은 C 언어에서 함수 호출로 진입할 수 있습니다.
{: .prompt-info }

위 `Main.c` 파일은 **100MB 메모리 주소 영역(0x640 0000)**에 의미 없는 값을 쓰는 동작을 수행합니다.

위 코드에서는 `long` 타입의 크기인 **4**를 메모리에 저장합니다. 32비트 ARM 머신이므로 숫자 **4**가 메모리에 저장되어야 합니다.

### C 언어 파일을 컴파일하기 위한 Makefile 📄

```bash
ARCH = armv7-a
MCPU = cortex-a8

CC = arm-none-eabi-gcc
AS = arm-none-eabi-as
LD = arm-none-eabi-ld
OC = arm-none-eabi-objcopy

LINKER_SCRIPT = ./navilos.ld
MAP_FILE = build/navilos.map

ASM_SRCS = $(wildcard boot/*.S)
ASM_OBJS = $(patsubst boot/%.S, build/%.os, $(ASM_SRCS))

C_SRCS = $(wildcard boot/*.c)
C_OBJS = $(patsubst boot/%.c, build/%.o, $(C_SRCS))

INC_DIRS  = -I include

navilos = build/navilos.axf
navilos_bin = build/navilos.bin

.PHONY: all clean run debug gdb

all: $(navilos)

clean:
	@rm -fr build
	
run: $(navilos)
	qemu-system-arm -M realview-pb-a8 -kernel $(navilos)
	
debug: $(navilos)
	qemu-system-arm -M realview-pb-a8 -kernel $(navilos) -S -gdb tcp::1234,ipv4
	
gdb:
	gdb-multiarch $(navilos)
	
$(navilos): $(ASM_OBJS) $(C_OBJS) $(LINKER_SCRIPT)
	$(LD) -n -T $(LINKER_SCRIPT) -o $(navilos) $(ASM_OBJS) $(C_OBJS) -Map=$(MAP_FILE)
	$(OC) -O binary $(navilos) $(navilos_bin)
	
build/%.os: boot/%.S
	mkdir -p $(shell dirname $@)
	$(CC) -march=$(ARCH) -mcpu=$(MCPU) $(INC_DIRS) -c -g -o $@ $<
	
build/%.o: boot/%.c
	mkdir -p $(shell dirname $@)
	$(CC) -march=$(ARCH) -mcpu=$(MCPU) $(INC_DIRS) -c -g -o $@ $<
```

먼저 **10번째 줄**에서 map 파일 이름을 지정했습니다. (`map` 파일은 링커가 생성하는 파일입니다.)

C 언어 소스코드와 오브젝트 파일을 변수로 지정한 후, 링커에 파라미터로 전달합니다.

마지막 `build/%.o`에서 C 언어 파일을 컴파일해 오브젝트 파일을 생성하는 매크로를 작성했습니다.

### 💡 `stdint.h` 파일 추가 필요 📄

```c
/* stdint.h 내용 (생략) */
```

빌드 후 **gdb**로 **100MB 위치(0x640 0000)**의 메모리 값을 확인합니다.

![메모리 값 확인 이미지](/assets/img/OS/os305.png)

`x/8wx 메모리 주소` 명령어로 메모리 값을 확인한 결과, **4**가 정상적으로 저장된 것을 확인할 수 있습니다.

## 요약 📝

***

이번 장에서는 시스템의 전원이 켜졌을 때의 **부팅 과정**과 펌웨어 초기화 코드를 작성하였습니다.

ARM 프로세서의 초기 진입 코드를 구현하고, 앞으로 만들 **나빌로스(Navilos)**의 메모리 맵을 구성하였습니다.

다음 장에서는 하드웨어 중 **UART**를 제어하여 **gdb**가 아닌 **터미널 화면**과의 상호작용을 구현할 예정입니다.

***


## 참고
***

참고 깃허브 : [https://github.com/navilera/Navilos](https://github.com/navilera/Navilos)

이만우 저자님의 블로그 주소 : [https://kldp.org/node/162560](https://kldp.org/node/162560)
